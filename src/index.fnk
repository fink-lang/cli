{yargs, config, option, usage, version, help} = import `./yargs`

{compile_all} = import `./compile`


main = fn process:
  # TODO: remove `node, fnk` once supported by larix
  [,, ...args] = process.argv

  {argv} = pipe yargs:: args:
    config:: {`camel-case-expansion`: false}
    version:: false

    usage:: `$0 --src file-or-dir [...] [--out-dir ouput-dir]`

    option:: `src`, {
        array: true,
        describe: `The directory containing files to compile.`
      }

    option:: `out-dir`, {
        describe: `The directory to generate code in.`,
        default: null
      }

    option:: `ignore`, {
        describe: `Glob paths/patterns for files to ignore`,
        default: null
      }

    help()

  [err] = attempt:
    compile_all:: process, argv.src, argv.`out-dir`, argv.ignore

  match true:
    err != null:
      console.error(err)
      process.exit(-1)

match require.main:
  # istanbul ignore next
  module: main(process)



