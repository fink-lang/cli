{rmdirSync, writeFileSync} = import 'fs'
{describe, it, expect, mock, mock_fn, to_match_snapshot, was_called_with, to_equal} = import '@fink/jest/test.fnk'
{length} = import '@fink/std-lib/iter.fnk'


mock './logging.fnk', fn:
  dict:
    logger: fn: dict:
      warn: mock_fn _
      error: mock_fn _

{readdirSync} = import 'fs'

{compile_all} = import './compile.fnk'



describe compile_all, fn:

  it 'compiles files to out-dir', fn:
    stdout = {write: mock_fn _}
    rmdirSync './build/test', {recursive: true}

    [...result] = compile_all {stdout}, ['./src'], './build/test'

    files = readdirSync './build/test'

    expect
      length result
      to_equal 8

    expect
       files
       to_match_snapshot


  it 'ignores files', fn:
    stdout = {write: mock_fn _}
    rmdirSync './build/test', {recursive: true}

    [...result] = compile_all
      {stdout}
      ['./src'], './build/test', './src/*.test.fnk'

    files = readdirSync './build/test'

    expect
      length result
      to_equal 4

    expect
       files
       to_match_snapshot


  it 'compiles as es6 module with inline source-maps to stdout', fn:
    stdout = {write: mock_fn _}
    writeFileSync
      './build/test-file.fnk'
      "
        {foo} = import '@fink/test'
        bar = foo * 2
      "

    [...result] = compile_all
      {stdout}
      ['./build/test-file.fnk'], false, false
      {source_maps: 'inline'}

    expect
      length result
      to_equal 1

    expect
      stdout.write.mock.calls
      to_match_snapshot


  it 'compiles as es6 module without source-maps to stdout', fn:
    stdout = {write: mock_fn _}
    writeFileSync
      './build/test-file.fnk'
      "
        {foo} = import '@fink/test/foo.fnk'
        bar = foo * 2
      "

    [...result] = compile_all
      {stdout}
      ['./build/test-file.fnk'], false, false
      {source_maps: false}

    expect
      length result
      to_equal 1

    expect
      stdout.write
      was_called_with '
        import { foo } from "@fink/test/foo.js";
        export const bar = foo * 2;
      '


  it 'compiles as cjs module without source-maps to stdout', fn:
    stdout = {write: mock_fn _}
    writeFileSync
      './build/test-file.fnk'
      "
        {foo} = import '@fink/test'
        bar = foo * 2
      "

    [...result] = compile_all
      {stdout}
      ['./build/test-file.fnk'], false, false
      {source_maps: false, module_type: 'cjs'}

    expect
      length result
      to_equal 1

    expect
      stdout.write
      was_called_with '
        const {
          foo
        } = require("@fink/test");

        const bar = foo * 2;
        exports.bar = bar;
      '

  it 'handles parse errors', fn:
    stdout = {write: mock_fn _}
    writeFileSync './build/test-file.fnk', 'foo bar ['

    {errors} = pipe compile_all {stdout}, ['./build/test-file.fnk']:
      find {errors}:
        match errors:
          []: true
          else: false

    expect
      length errors
      to_equal 1

